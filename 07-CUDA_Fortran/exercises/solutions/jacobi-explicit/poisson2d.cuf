!
!  Copyright 2015 NVIDIA Corporation
!
!  Licensed under the Apache License, Version 2.0 (the "License");
!  you may not use this file except in compliance with the License.
!  You may obtain a copy of the License at
!
!      http://www.apache.org/licenses/LICENSE-2.0
!
!  Unless required by applicable law or agreed to in writing, software
!  distributed under the License is distributed on an "AS IS" BASIS,
!  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
!  See the License for the specific language governing permissions and
!  limitations under the License.
!
module kernels
  contains
  !! TODO: write kernels subroutine to be lunch on the device using global attribute
    attributes(global) subroutine calcKernel( A_dev, Anew_dev)
         implicit none
         real(8) :: A_dev(:,:),Anew_dev(:,:)
         integer :: i,j,n(2)
        
         i = blockDim%x * (blockIdx%x - 1) + threadIdx%x
         j = blockDim%y * (blockIdx%y - 1) + threadIdx%y
         n(1) = size(A_dev,1)
         n(2) = size(A_dev,2)
        
         if (j>1 .and. j <n(2) .and. i>1 .and. i <n(1) ) then
           Anew_dev(i,j) = 0.25 * ( A_dev(i+1,j) + A_dev(i-1,j) + A_dev(i,j-1) + A_dev(i,j+1) )
         end if

      end subroutine calcKernel


end module kernels 



program main
use kernels
use cudafor 
! use omp_lib
implicit none

double precision, external :: WTime ! from common.f90
integer, external :: poisson2d_reference
logical, external :: check_results

integer, parameter :: NN = 4096, NM = 4096

real(8), allocatable :: A(:,:), Aref(:,:), Anew(:,:)
!! TODO: Define device arrays (A_dev,Anew_dev ...) 
real(8),device, allocatable :: A_dev(:,:), Anew_dev(:,:)
real(8) :: t1, t2, t1_ref, t2_ref, runtime, runtime_ref, pi, y0, tol

integer :: i, j, num_openmp_threads, iter, iter_max, ist

!! TODO: Define dim3 variables for lunch configuration 
type(dim3) :: gridDim, blockDim

  iter_max = 500
  pi    = 2.d0 * asin(1.d0)
  tol   = 1.d-5

  allocate(A(NN,NM), Aref(NN,NM), Anew(NN,NM), stat=ist)
  !! TODO: Allocate device arrays
  allocate(A_dev(NN,NM), Anew_dev(NN,NM), stat=ist)
  if (ist /= 0) stop "failed to allocate arrays!"


! set boundary conditions
  do j = 1, NM
    y0 = sin( 2.0 * pi * j / (NM-1))

    A(1,j)    = y0
    A(NN,j)   = y0

    Aref(1,j) = y0
    Aref(NN,j)= y0

    Anew(1,j) = y0
    Anew(NN,j)= y0

  end do ! j

  write(*,fmt="('Jacobi relaxation Calculation: ',i0,' x ',i0,' mesh')") NN,NM

  write(*,fmt="('Calculate reference solution and time CPU execution.')")
  write(*,fmt="('')")
  write(*,fmt="('   Iter')")
  write(*,fmt="('--------')")
  t1_ref = Wtime()
  num_openmp_threads = poisson2d_reference(iter_max, tol, Anew, Aref, NM, NN)
  t2_ref = Wtime()
  runtime_ref = t2_ref-t1_ref
  write(*,fmt="('GPU execution.')")




  t1 = Wtime()

  iter = 0
 
!! TODO: copy data to device.

  A_dev = A

!! TODO: Configure lunch parameters using dim3 type variables
  blockDim = dim3(32,32,1)
  gridDim = dim3(ceiling(real(NN)/blockDim%x), ceiling(real(NM)/blockDim%y), 1)

do while (iter < iter_max) 

!! TODO: lunch kernels. Remember you have two sets (host and device) of arrays! 
  call calcKernel<<<gridDim,blockDim>>>(A_dev,Anew_dev)
! Swap input/output
!! TODO:  swap input / output on the device. Remember you have two sets (host and device) of arrays!   
  A_dev(2:NN-1,2:NM-1) = Anew_dev(2:NN-1,2:NM-1)
! Periodic boundary conditions
!! TODO: apply periodic boundary conditions on device. Remember you have two sets (host and device) of arrays! 
  A_dev(2:NN-1,1)  = A_dev(2:NN-1,NM-1)
  A_dev(2:NN-1,NM) = A_dev(2:NN-1,2)

  if(mod(iter,100) == 0) write(*,fmt="(2x,i4)") iter

  iter = iter + 1
end do ! while
!! TODO: Copy the data to the host
A = A_dev


t2 = Wtime()
runtime = t2-t1
if (check_results(Aref, A, NM, NN, tol)) write(*,fmt="(i0,' x ',i0,': ','1 GPU: ',f0.4,'s, ',i0,' CPU cores: ',f0.4,'s',', speedup: ',f0.2)") NN, NM, runtime, num_openmp_threads,runtime_ref,runtime_ref/runtime


end program ! main
